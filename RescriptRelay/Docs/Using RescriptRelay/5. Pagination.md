# 페이지네이션

### 읽을 거리

- GraphQL에서의 쿼리와 뮤테이션
- 릴레이의 가이드 투어: 데이터 리스트 렌더링과 페이지네이션
- 릴레이 서버 스펙: 커넥션
- 리액트 문서: 데이터 페칭을 위한 서스펜스

## 릴레이의 페이지네이션

> 본 페이지에서 설명하는 기능은 릴레이 스펙을 준수하는 스키마를 요구합니다.

릴레이는 커넥션 기반 페이지네이션과 릴레이 서버 스펙을 준수하는 경우 페이지네이션을 매우 쉽게 할 수 있는 내장 도구들을 가지고 있습니다.

### 페이지네이션을 위한 준비

페이지네이션은 항상 `프라그먼트`를 통해 사용됩니다.
다음은 `ticketsConnection`을 따라 페이지네이션을 수행하는 Relay 프라그먼트의 예시입니다.

```re
module Fragment = %relay(`
  fragment RecentTickets_qeury on Query
    @refetchable(queryName:"RecentTicketsRefetchQuery")
    @argumentDefinitions(
      count: {type:"Int!", defaultValue:10},
      cursor:{type:"String!",defaultValue:"}
    ){
      ticketConnection(first:$count,after:$cursor)
      @connection(key:"RecentTickets_ticketsConnection")
      {
        edges{
          node{
            id
            ...SingleTicket_ticket
          }
        }
      }
    }
`)
```

몇 개의 지시어와 어노테이션이 사용되었습니다. 하나씩 살펴보겠습니다.

1. 우선, 이 프라그먼트는 `Query` 루트 타입(루트 쿼리 타입은 아무 GraphQL 타입과 동일합니다)에 정의되었습니다.
이는 `ticketsConnection` 필드가 `Query`에 있어야 하기 때문입니다. 
페이지네이션은 아무 GraphQL 타입의 필드에 사용될 수 있습니다.

2. `@refetchable` 지시어를 통해서 이 fragment가 다시 호출될 수 있도록 하였습니다.
3. `@argumentDefinitions` 지시어를 추가하여 페이지네이션에서 필요한 인자인 `count`와 `cursor`를 정의하였습니다.
따라서 이 컴포넌트는 스스로 페이지네이션을 수 있고, 이 인자를 초기에 제공하지 않더라도 누구나 이 프라그먼트를 사용할 수 있기를 바랍니다.
4. 쿼리의 `ticketsConnection` 필드를 선택해서 페이지네이션 인자를 전달하였습니다.
또한 필드에 `@connection` 지시어를 추가했습니다.
이는 매우 중요한데, 이는 Relay에게 우리가 이 특정한 필드에 대해 페이지네이션을 도와달라고 전달하는 것이기 때문입니다.
`@connection`으로 표기하고, `keyName`을 전달함으로써, 릴레이는 페이지네이션을 위해 필드를 어떻게 가져올 지 이해할 수 있습니다.
이것은 수 많은 페이지네이션을 위한 수 많은 훅들과 함수들에 접근이 가능하고, 페이지네이션을 저장소에서 다룰 수 있음을 의미합니다.
`@connection`에 관해서는 [여기](https://relay.dev/docs/glossary/#connection)서 더 확인할 수 있습니다.
5. 마지막으로, 다른 컴포넌트의 프라그먼트인 `SingleTicket_ticket`을 커넥션의 노드에 뿌렸습니다.
이는 우리가 각 티켓별로 보여줄 컴포넌트입니다.

이제 이 프라그먼트를 페이지네이션하기 위한 모든 것들을 알게 되었습니다

## 컴포넌트에서의 페이지네이션

위의 페이지네이션을 위한 프라그먼트를 사용하는 컴포넌트를 확인해보세요.

```re
@react.component
let make = (~query) => {
  let {data,hasNext,isLoadingNext,loadNext} = Fragment.usePagination(query)
  let tickets = data.ticketsConnection -> Fragment.getConnectionNodes

  <div className="card">
    <div className="card-body">
      <h4 className="card-title"> {React.string("Recent Tickets")} </h4>
      <div>
        {tickets
        ->Belt.Array.map(ticket => <SingleTicket key=ticket.id ticket=ticket.fragmentRefs />)
        ->React.array}
        {hasNext
          ? <button onClick={_ => loadNext(~count=2, ()) |> ignore} disabled=isLoadingNext>
              {React.string(isLoadingNext ? "Loading..." : "More")}
            </button>
          : React.null}
      </div>
    </div>
  </div>
}
```

위의 내용들을 하나씩 살펴봅시다. 

1. 아무 프라그먼트를 다루듯, 여기서도 프라그먼트에 넘겨줄 프라그먼트 레퍼런스가 필요합니다.
2. 프라그먼트의 레퍼런스를 `Fragment.usePagination` 에 넘겨줍니다. 
이는 페이지네이션을 도울 함수와 속성들이 들어있는 레코드를 반환할 것입니다.
3. RescriptRelay는 자동으로 커넥션을 사용할 수 있는 노드의 배열로 변환합니다.
이를 자동생성된 함수를 통해 모든 노드를 모으고(getConnectionNodes), 이를 컴포넌트에 사상하여 렌더링할 수 있습니다.
4. 각 노드를 재호출 가능한 노드의 데이터를 요구하는 `<SingleTicket/>` 컴포넌트로 렌더링하고 있습니다.
5. 마지막으로, `usePagination`에서 제공하는 헬퍼 함수를 사용하여 만약 더 불러올 데이터가 있다면 더 불러오기 버튼을 만들고, 이미 데이터를 불러오는 중이라면 비활성화를 시켰습니다.

이제 기본적인 페이지네이션을 수행했습니다.

## 두 종류의 페이지네이션

릴레이는 기본적으로 두 종류의 페이지네이션을 제공합니다.
 1. "일반적인" 페이지네이션.
 위의 `usePagination`과 같이 사용하는 것을 의미합니다.
 이 페이지네이션은 suspense를 내장하지 않고, isLoadingNext와 isLoadingPrevious의 플래그를 제공하여 요청이 진행중인지를 지시합니다.

 2. _블로킹_ 페이지네이션.
 이는 `useBlockingPagination`을 사용하는 것이고, 이는 서스펜스를 내장하고 있습니다.
 이는 한번에 다 불러오기와 같은 페이지네이션에 적합합ㄴ디ㅏ.


